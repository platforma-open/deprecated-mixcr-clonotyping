// process

self := import("@platforma-sdk/workflow-tengo:tpl")

ll := import("@platforma-sdk/workflow-tengo:ll")
assets := import("@platforma-sdk/workflow-tengo:assets")
pframes := import("@platforma-sdk/workflow-tengo:pframes")
smart := import("@platforma-sdk/workflow-tengo:smart")
file := import("@platforma-sdk/workflow-tengo:file")
llPFrames := import("@platforma-sdk/workflow-tengo:pframes.ll")
pSpec := import("@platforma-sdk/workflow-tengo:pframes.spec")
pUtil := import("@platforma-sdk/workflow-tengo:pframes.util")
pConstants := import("@platforma-sdk/workflow-tengo:pframes.constants")

json := import("json")
text := import("text")

mixcrAnalyzeTpl := assets.importTemplate(":mixcr-analyze")

self.awaitState("InputsLocked")
self.awaitState("params", "ResourceReady")
self.awaitState("inputSpec", "ResourceReady")
self.awaitState("presetContent", "ResourceReady")
self.awaitState("pfconvParamsById", "ResourceReady")
self.awaitState("featureParams", "ResourceReady")

self.body(func(inputs) {
	params := inputs.params
	preset := inputs.preset
	inputSpec := inputs.inputSpec
	featureParams := inputs.featureParams
	clonotypeKeyAxes := inputs.clonotypeKeyAxes
	species := params.species
	limitInput := params.limitInput
	blockId := params.blockId
	presetCommonName := params.presetCommonName

	presetContent := inputs.presetContent.getDataAsJson()
	pfconvParamsById := inputs.pfconvParamsById
	pfconvParamsByKey := inputs.pfconvParamsByKey

	if is_undefined(presetContent) {
		ll.panic("no presetContent")
	}

	if is_undefined(pfconvParamsById) {
		ll.panic("no pfconvParams")
	}

	sampleAxis := inputSpec.axesSpec[0]
	trace := pSpec.makeTrace(inputSpec,
		{type: "milaboratories.mixcr-clonotyping", id: blockId, importance: 20, label: "MiXCR " + presetCommonName}
	)

	reports := []

	for step in presetContent.reportTypes {
		if step == "align" || step == "assemble" {
			reports = append(reports, {
				id: step,
				fileJson: "result." + step + ".report.json",
				fileTxt: "result." + step + ".report.txt"
			})
		}
	}

	hasAssembleContigs := false
	hasAssembleCells := false
	for stage in presetContent.analysisStages {
		if stage == "assembleContigs" {
			hasAssembleContigs = true
		} else if stage == "assembleCells" {
			hasAssembleCells = true
		}
	}

	cColumnsAdditionalAxesSpec := [sampleAxis]
	if !hasAssembleCells {
		cColumnsAdditionalAxesSpec = append(cColumnsAdditionalAxesSpec, {
			name: "pl7.app/vdj/chain",
			type: "String",
			annotations: {
				"pl7.app/label": "Chain",
				"pl7.app/axisNature": "scaleCompatible"
			}
		})
	}

	decompositionById := pUtil.decomposePfconvImportCfg(pfconvParamsById, {
		additionalAxesSpec: cColumnsAdditionalAxesSpec
	})
	purifiedPfconvByIdCfg := decompositionById.purifiedCfg
	cloneColumnsByIdSpec := decompositionById.columnsSpec

	decompositionByKey := pUtil.decomposePfconvImportCfg(pfconvParamsByKey, {
		additionalAxesSpec: cColumnsAdditionalAxesSpec
	})
	purifiedPfconvByKeyCfg := decompositionByKey.purifiedCfg
	cloneColumnsByKeySpec := decompositionByKey.columnsSpec

	fileExtension := inputSpec.domain["pl7.app/fileExtension"]

	aggregationTargets := [{
		type: "Resource",
		name: "qc"
	}, {
		type: "Resource",
		name: "log"
	}, {
		type: "Resource",
		name: "clns"
	}, {
		type: "ResourceMap",
		name: "reports",
		keyLength: 2 // [reportType, reportFormat]
	}]

	ll.assert(purifiedPfconvByIdCfg.partitionKeyLength == 0, "unexpected partition length")
	ll.assert(purifiedPfconvByIdCfg.storageFormat == "Binary", "unexpected format")

	innerPartitionLength := 0
	if !hasAssembleCells {
		innerPartitionLength = 1 // [chain]
	}

	for columnCfg in purifiedPfconvByIdCfg.columns {
		aggCfg := {
			type: "BinaryPartitioned",
			name: "clones/" + columnCfg.id,
			path: ["clones", columnCfg.id],
			partitionKeyLength: innerPartitionLength
		}
		aggregationTargets = append(aggregationTargets, aggCfg)
	}

	for columnCfg in purifiedPfconvByKeyCfg.columns {
		aggCfg := {
			type: "BinaryPartitioned",
			name: "clonesKey/" + columnCfg.id,
			path: ["clonesKey", columnCfg.id],
			partitionKeyLength: innerPartitionLength
		}
		aggregationTargets = append(aggregationTargets, aggCfg)
	}

	ll.assert(smart.isResource(inputs.inputData), "input data should be a resource")

	aggIndices := undefined
	if len(inputSpec.axesSpec) == 1 { // Fasta
		aggIndices = []
	} else if len(inputSpec.axesSpec) == 2 { // Fastq
		ll.assert(inputSpec.axesSpec[1].name == "pl7.app/sequencing/readIndex", "unexpected axis name " + inputSpec.axesSpec[1].name)
		aggIndices = [1]
	} else if len(inputSpec.axesSpec) == 3 { // MultilaneFastq
		ll.assert(inputSpec.axesSpec[1].name == "pl7.app/sequencing/lane", "unexpected axis name " + inputSpec.axesSpec[1].name)
		ll.assert(inputSpec.axesSpec[2].name == "pl7.app/sequencing/readIndex", "unexpected axi name " + inputSpec.axesSpec[2].name)
		aggIndices = [1, 2]
	} else {
		ll.panic("unexpected structure of input PColumn with sequencing data")
	}
	mixcrResults := llPFrames.aggregate(
		inputs.inputData, aggIndices, mixcrAnalyzeTpl,
		aggregationTargets,
		false,
		{
			preset: preset,
			params: {
				species: species,
				limitInput: limitInput,
				fileExtension: fileExtension,
				reports: reports,
				featureParams: featureParams
			},
			clonotypeKeyAxes: clonotypeKeyAxes,
			presetContent: presetContent,
			pfconvParamsById: purifiedPfconvByIdCfg,
			pfconvParamsByKey: purifiedPfconvByKeyCfg
		}
	)

	clones := pframes.pFrameBuilder()
	for columnId, columnSpec in cloneColumnsByIdSpec {
		clones.add(columnId, trace.inject(columnSpec), mixcrResults.output("clones/" + columnId))
	}
	if !hasAssembleCells {
		for columnId, columnSpec in cloneColumnsByKeySpec {
			clones.add(columnId, trace.inject(columnSpec), mixcrResults.output("clonesKey/" + columnId))
		}
	}
	clones.build()

	joinOrUndefined := func(arr) {
		if is_undefined(arr) {
			return undefined
		} else {
			return text.join(arr, ",")
		}
	}

	// calculating clns annotations

	removeUndefined := func(m) {
		r := {}
		for k, v in m {
			if !is_undefined(v) {
				r[k] = v
			}
		}
		return r;
	}

	clnsAnnotations := removeUndefined({
		"mixcr.com/assemblingFeature": joinOrUndefined(presetContent.assemblingFeature),
		"mixcr.com/cellTags": joinOrUndefined(presetContent.cellTags),
		"mixcr.com/coveredFeaturesOnExport": joinOrUndefined(presetContent.coveredFeaturesOnExport),
		"mixcr.com/umiTags": joinOrUndefined(presetContent.umiTags),
		"pl7.app/label": "MiXCR Clonesets"
	})
	if hasAssembleContigs {
		clnsAnnotations["mixcr.com/contigsAssembled"] = "true"
	}
	if hasAssembleCells {
		clnsAnnotations["mixcr.com/cellsAssembled"] = "true"
	}

	return {
		"qc.spec": trace.inject({
			kind: "PColumn",
			name: "mixcr.com/qc",
			domain: {
				"pl7.app/blockId": blockId
			},
			valueType: "File",
			axesSpec: [
				inputSpec.axesSpec[0]
			]
		}),
		"qc.data": mixcrResults.output("qc"),

		"logs.spec": trace.inject({
			kind: "PColumn",
			name: "pl7.app/log",
			domain: {
				"pl7.app/blockId": blockId
			},
			valueType: "Log",
			axesSpec: [
				inputSpec.axesSpec[0]
			]
		}),
		"logs.data": mixcrResults.output("log"),

		"reports.spec": trace.inject({
			kind: "PColumn",
			name: "mixcr.com/report",
			domain: {
				"pl7.app/blockId": blockId
			},
			valueType: "File",
			axesSpec: [
				inputSpec.axesSpec[0],
				{
					type: "String",
					name: "mixcr.com/report/source",
					annotations: {
						"pl7.app/label": "Source MiXCR stage"
					}
				},
				{
					type: "String",
					name: "mixcr.com/report/format",
					annotations: {
						"pl7.app/label": "Report format"
					},
					domain: {
						"pl7.app/dense": string(json.encode(["json", "txt"]))
					}
				}
				]
		}),
		"reports.data": mixcrResults.output("reports"),

		"clns.spec": trace.inject({
			kind: "PColumn",
			name: "mixcr.com/clns",
			domain: {
				"pl7.app/blockId": blockId
			},
			annotations: clnsAnnotations,
			valueType: "File",
			axesSpec: [ inputSpec.axesSpec[0] ]
		}),
		"clns.data": mixcrResults.output("clns"),
		
		clones: clones
	}
})
