// get preset

self := import("@milaboratory/tengo-sdk:tpl")
ll := import("@milaboratory/tengo-sdk:ll")
exec := import("@milaboratory/tengo-sdk:exec")
assets := import("@milaboratory/tengo-sdk:assets")

self.validateInputs({
	"__options__,closed": "",
	"request": {
		"__options__,closed": "",
		"presetName": "string",
		"species,omitempty": "string"
	}
})

mixcrSw := assets.importSoftware("@milaboratory/mixcr:main")

self.defineOutputs("preset")

self.body(func(inputs) {
	presetName := inputs.request.presetName
	species := inputs.request.species

	mixcrCmdBuilder := exec.builder().
						software(mixcrSw).
						printErrStreamToStdout().
						arg("exportPreset").
						arg("--preset-name").
						arg(presetName)

	if !is_undefined(species) {
		mixcrCmdBuilder.arg("--species").arg(species)
	}

	mixcrCmd := mixcrCmdBuilder.arg("preset.json").
						saveFileContent("preset.json").
						run()

	presetContent := mixcrCmd.getFileContent("preset.json")

	return {
		preset: presetContent
	}
})
